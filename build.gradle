import java.nio.file.Paths

plugins {
  id "java"
  id "idea"
  id "maven"
  id "eclipse"
  id "com.moowork.node" version "1.3.1"
  id "com.github.ben-manes.versions" version "0.25.0"
  id "org.springframework.boot" version "2.1.8.RELEASE"
  id "io.spring.dependency-management" version "1.0.8.RELEASE"
}

allprojects {
  group = "daggerok"
  version = "0.0.2-SNAPSHOT"
  description = "Spring Boot 5 Reactive Angular ngrx"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  // mavenLocal()
  mavenCentral()
}

dependencies {
  implementation("org.springframework.boot:spring-boot-starter-actuator")
  implementation("org.springframework.boot:spring-boot-starter-thymeleaf")
  implementation("org.springframework.boot:spring-boot-starter-webflux")
  // runtime("org.springframework.boot:spring-boot-devtools")
  compileOnly("org.springframework.boot:spring-boot-configuration-processor")
  testCompileOnly("org.springframework.boot:spring-boot-configuration-processor")
  testImplementation("org.springframework.boot:spring-boot-starter-test")
  testImplementation("io.projectreactor:reactor-test")
}

wrapper {
  gradleVersion = "5.6.2"
  distributionType = "BIN"
}

node {
  download = true
  version = "12.10.0"
  npmVersion = "6.11.3"
  nodeModulesDir = Paths.get("src", "main", "js").toFile()
}

assemble.dependsOn npm_run_build
npm_run_build.dependsOn npm_i
npm_run_lint.dependsOn npm_i

clean.doFirst {
  ["nodejs", "npm"].each { delete "$projectDir/.gradle/$it" }
  ["out", "build"].each { delete "$projectDir/$it" }
  delete Paths.get("${node.nodeModulesDir.path}", "node_modules")
}

defaultTasks "clean", "build"
